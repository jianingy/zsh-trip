# PSVAR
# - [1] :: vcs info
# - [2] :: SSH_CONNECTION HOST
function prompt_powerline_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  vcs_info

  # Xfer vcs info through psvar
  psvar=()
  [[ -n $vcs_info_msg_0_ ]] && psvar[1]=$(print -P "$vcs_info_msg_0_" |sed "s/\^\[/\033/")
  if [[ -n "$SSH_CONNECTION" ]]; then
     ssh_from=(${=SSH_CONNECTION})
	 psvar[2]=" ← $ssh_from[1]"
  fi
}

function prompt_powerline_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook vcs_info


  zstyle ':vcs_info:*' stagedstr ' ◯'
  zstyle ':vcs_info:*' unstagedstr ' ●'
  zstyle ':vcs_info:*' formats       '%s:%b%c%u'
  zstyle ':vcs_info:*' actionformats '%s:%b[%a] %c%u'
  zstyle ':vcs_info:*' check-for-changes true

  narrow=⮀
  rarrow=⮂
  color1=${1:-172}
  color2=${2:-238}
  color3=${3:-118}
  color4=${4:-24}
  activefg=233

  p_vcs="%(1V.%K{$color4}$narrow%f %1v %F{$color4}%k$narrow.$narrow)"

  prompt="%F{$activefg}%K{$color1} %n at %U%M%u %k%F{$color1}%K{$color2}$narrow%f %~ %k%F{$color2}$p_vcs %f"
  RPS1=" %F{$color2}$rarrow%f%K{$color2} %j %F{$color1}$rarrow%F{233}%K{$color1}%(2V.%2v.) "
  PS2="%F{$activefg}%K{$color3} %_ %k%F{$color3}$narrow%f%k "

  add-zsh-hook precmd prompt_powerline_precmd
}

prompt_powerline_setup "$@"
